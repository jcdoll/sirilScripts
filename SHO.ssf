requires 1.2.0

close

# Ref
# https://siril.readthedocs.io/en/stable/Commands.html


# Load, register and stack SHO images
cd S
convert S -out=../process
cd ../process

calibrate S -bias=../../../../calibration/masters/bias_stacked -flat=../../../../calibration/masters/flat_stacked
# calibrate S -bias=../masters/bias_stacked -dark=../masters/pp_dark_stacked -flat=../masters/pp_flat_stacked -cc=dark

# remove background gradient
seqsubsky S 1
register bkg_S -2pass 

# 1.8 sigma FWHM outlier removal
seqapplyreg bkg_S -filter-wfwhm=1.8k

# winsorized rejection (default) at +/- 3 sigma
stack r_bkg_S rej w 3 3 -norm=addscale -fastnorm -out=../stacks/stack_S
cd ..

cd H
convert H -out=../process
cd ../process
calibrate H -bias=../../../../calibration/masters/bias_stacked -flat=../../../../calibration/masters/flat_stacked
seqsubsky H 1
register bkg_H -2pass 
seqapplyreg bkg_H -filter-wfwhm=1.8k
stack r_bkg_H rej w 3 3 -norm=addscale -fastnorm -out=../stacks/stack_H
cd ..

cd O
convert O -out=../process
cd ../process
calibrate O -bias=../../../../calibration/masters/bias_stacked -flat=../../../../calibration/masters/flat_stacked
seqsubsky O 1
register bkg_O -2pass 
seqapplyreg bkg_O -filter-wfwhm=1.8k
stack r_bkg_O rej w 3 3 -norm=addscale -fastnorm -out=../stacks/stack_O
cd ..

# Register stacks
cd stacks
convert stacks
register stacks -2pass
seqapplyreg stacks -framing=min

# TODO: Do we even need this? Should we just output with the correct names above
# 1 is H, 2 is O, 3 is S in alphabetical order
# Linear match O and S to H at the same time before saving
# https://siril.org/tutorials/rgb_composition/#2-using-pixel-math
# https://siril.readthedocs.io/en/latest/processing/lmatch.html
load r_stacks_00001
#autostretch
save H

load r_stacks_00002
#linear_match H 0 0.92
save O

load r_stacks_00003
#linear_match H 0 0.92
save S

# Save an SHO composite image
rgbcomp S H O -out=sho
load sho
save ..\unstretched_sho

# Save stretched SHO for reference
# We use the unstretched SHO files below for pixelmath
# Autostretch without args uses unlinked channels, which affects white balance, so don't do this after any color calibration
autostretch
save ..\stretched_sho

# generate RGB from SHO
# ref: https://siril.org/tutorials/rgb_composition/
# ref: https://siril.org/tutorials/pixelmath/
# ref: https://thecoldestnights.com/2020/06/pixinsight-dynamic-narrowband-combinations-with-pixelmath/
# ref: https://telescope.live/blog/dynamic-narrowband-combinations-pixelmath
#pm "($O$^~$O$)*$S$ + ~($O$^~$O$)*$H$"
#save R
#pm "(($O$*$H$)^~($O$*$H$))*$H$ + ~(($O$*$H$)^~($O$*$H$))*$O$"
#save G
#pm "$O$"
#save B

# Alt coloring (plain vanilla)
#pm "$S$"
#save R
#pm "$H$"
#save G
#pm "$O$"
#save B

# Basic HOO
pm "$H$"
save R
pm "$O$"
save G
pm "$O$"
save B

# Natural color HOO blend
pm "$H$"
save R
pm "0.2*$H$ + 0.8*$O$"
save G
pm "0.15*$H$ + 0.85*$O$"
save B

# Blue/red SHO mix
# https://www.cloudynights.com/topic/557828-pixinsights-narrow-band-combinations-with-pixelmath/
pm "0.76*$H$ + 0.24*$S$"
save R
pm "$O$"
save G
pm "0.85*$O$ + 0.15*$H$"
save B

# Blue/gold
# https://jonrista.com/the-astrophotographers-guide/pixinsights/narrow-band-combinations-with-pixelmath-hoo/
pm "$H$"
save R
pm "0.85*$H$ + 0.15*$O$"
save G
pm "0.15*$H$ + 0.85*$O$"
save B

# Compose and output
rgbcomp R G B -out=rgb
load rgb
save ..\unstretched_rgb

# reduce noise using defaults
# this can lead to background blotches
# denoise
# save ..\unstretched_rgb_denoised

# save original version
autostretch

# midtone transfer function, low/mid/high
mtf 0.20 0.5 1.0

# increase saturation by 100%, with no threshold for background (default for 2nd arg = 1)
satu 1 0

save ..\output
savetif ..\output -astro -deflate
savejpg ..\output 90

# save star free version
load ../unstretched_rgb
starnet -stretch
autostretch
mtf 0.20 0.5 1.0
satu 1 0
save ..\output_noStars
savetif ..\output_noStars -astro -deflate
savejpg ..\output_noStars 90




